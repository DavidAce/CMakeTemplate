function(pad_string OUT_VARIABLE DESIRED_LENGTH FILL_CHAR VALUE)
    string(LENGTH "${VALUE}" VALUE_LENGTH)
    math(EXPR REQUIRED_PADS "${DESIRED_LENGTH} - ${VALUE_LENGTH}")
    set(PAD ${VALUE})
    if(REQUIRED_PADS GREATER 0)
        math(EXPR REQUIRED_MINUS_ONE "${REQUIRED_PADS} - 1")
        foreach(FOO RANGE ${REQUIRED_MINUS_ONE})
            set(PAD "${PAD}${FILL_CHAR}")
        endforeach()
    endif()
    set(${OUT_VARIABLE} "${PAD}" PARENT_SCOPE)
endfunction()

function(remove_genexpr list_data)
    if(NOT ${list_data})
        return()
    endif()

#    string(REGEX REPLACE "\\$<[A-z]*:?|>|:>" "" list_data_rgxp "${${list_data}}")
    string(REGEX REPLACE "((\\$<)+[A-z]*:)+|:;>|:>|>" "" list_data_rgxp "${${list_data}}")
#    string(REGEX REPLACE "\\$<[A-z]*:?|>|:>" "" list_data_rgxp "${list_data_rgxp1}")


#    message("list_data ${list_data} | ${${list_data}} | ${list_data_rgxp}")
    foreach(elem ${list_data_rgxp})
        if(elem MATCHES "TYPE|Release|MinSizeRel|RelWithDebInfo|Debug")
#            message("Discarding ${elem}" )
            continue()
        else()
#            message("Adding ${elem}" )
            list(APPEND match_list ${elem})
        endif()
    endforeach()
    set(${list_data} ${match_list} PARENT_SCOPE)
endfunction()

function(print_target_info target_name prefix)
    if(TARGET ${target_name})
        get_target_property(INFO_INC  ${target_name} INTERFACE_INCLUDE_DIRECTORIES)
        get_target_property(INFO_INT  ${target_name} INTERFACE_LINK_LIBRARIES)
        get_target_property(INFO_OPT  ${target_name} INTERFACE_COMPILE_OPTIONS)
        get_target_property(INFO_DEF  ${target_name} INTERFACE_COMPILE_DEFINITIONS)
        get_target_property(INFO_FTR  ${target_name} INTERFACE_COMPILE_FEATURES)
        get_target_property(TYPE_IMP  ${target_name} IMPORTED)
        get_target_property(TYPE_LNK  ${target_name} TYPE)
        if(TYPE_IMP)
            get_target_property(INFO_IMP ${target_name} IMPORTED_LOCATION)
        endif()
        if(TYPE_LNK MATCHES "STATIC|SHARED|UNKNOWN" AND NOT TYPE_LNK MATCHES "INTERFACE")
            get_target_property(INFO_LOC  ${target_name} LOCATION)
        endif()
        if(TYPE_LNK MATCHES "STATIC|SHARED|UNKNOWN|OBJECT|EXECUTABLE" AND NOT TYPE_LNK MATCHES "INTERFACE")
            get_target_property(INFO_LIB ${target_name} LINK_LIBRARIES)
        endif()
#        if(${target_name} MATCHES "CONAN")
#
#        endif()
        remove_genexpr(INFO_INC)
        remove_genexpr(INFO_INT)
        remove_genexpr(INFO_LIB)
        remove_genexpr(INFO_OPT)
        remove_genexpr(INFO_DEF)
        remove_genexpr(INFO_FTR)

        if(INFO_INT STREQUAL INFO_LIB)
            unset(INFO_INT)
        endif()


        pad_string(padded_target "40" " " "${prefix}[${target_name}]" )
        if(INFO_LIB)
            list(REMOVE_DUPLICATES INFO_LIB)
            message(STATUS "${padded_target} LIBRARY : ${INFO_LIB}" )
        endif()
        if(INFO_INT)
            list(REMOVE_DUPLICATES INFO_INT)
            message(STATUS "${padded_target} INTRFCE : ${INFO_INT}" )
        endif()
        if(INFO_INC)
            list(REMOVE_DUPLICATES INFO_INC)
            message(STATUS "${padded_target} INCLUDE : ${INFO_INC}" )
        endif()
        if(INFO_OPT)
            list(REMOVE_DUPLICATES INFO_OPT)
            message(STATUS "${padded_target} OPTIONS : ${INFO_OPT}" )
        endif()
        if(INFO_DEF)
            list(REMOVE_DUPLICATES INFO_DEF)
            message(STATUS "${padded_target} DEFINES : ${INFO_DEF}" )
        endif()
        if(INFO_FTR)
            message(STATUS "${padded_target} FEATURE : ${INFO_FTR}" )
        endif()
        if(INFO_IMP)
            message(STATUS "${padded_target} IMPORTS : ${INFO_IMP}" )
        elseif(INFO_LOC)
            message(STATUS "${padded_target} IMPORTS : ${INFO_LOC}" )
        endif()
    endif()
endfunction()


function(print_compiler_info prefix)
    pad_string(padded_string "40" " " "${prefix}[${CMAKE_CXX_COMPILER_ID} ${CMAKE_CXX_COMPILER_VERSION}]" )
    if(CMAKE_BUILD_TYPE STREQUAL "Release")
        message(STATUS "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_RELEASE}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "Debug")
        message(STATUS "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_DEBUG}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "RelWithDebInfo")
        message(STATUS "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_RELWITHDEBINFO}" )
    endif()
    if(CMAKE_BUILD_TYPE STREQUAL "MinSizeRel")
        message(STATUS "${padded_string} OPTIONS : ${CMAKE_CXX_FLAGS} ${CMAKE_CXX_FLAGS_MINSIZEREL}" )
    endif()
endfunction()